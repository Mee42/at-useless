buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.61'
}
repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}
def ktor_version = '1.3.0-rc'
def logback_version = '1.2.3'
def jdbi_version = '3.11.1'

kotlin {
    jvm()
    js {
        browser {
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {

            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "io.ktor:ktor-html-builder:$ktor_version"
                implementation "io.ktor:ktor-gson:$ktor_version"

                implementation "ch.qos.logback:logback-classic:$logback_version"

                implementation "org.yaml:snakeyaml:1.21" // parsing yaml files, for the param.yaml file
                implementation "com.google.code.gson:gson:2.8.6"

                implementation "org.jdbi:jdbi3-core:$jdbi_version"
                implementation "org.jdbi:jdbi3-sqlobject:$jdbi_version"
                implementation "org.jdbi:jdbi3-kotlin:$jdbi_version"
                implementation "org.jdbi:jdbi3-kotlin-sqlobject:$jdbi_version"
                implementation "org.xerial:sqlite-jdbc:3.28.0"

                implementation "at.favre.lib:bcrypt:0.9.0" // bcrypt library
            }
            resources {
                srcDir = "src/jvmMain/resources"
            }
        }
        jvmTest {
            targetCompatibility = JavaVersion.VERSION_1_8
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "io.ktor:ktor-client-js:$ktor_version"
                implementation "io.ktor:ktor-client-core:$ktor_version"
                api npm("text-encoding","0.7.0")
                api npm("bufferutil","4.0.1")
                api npm("abort-controller", "3.0.0")
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

jvmJar {
    dependsOn(jsBrowserWebpack)
    from(new File(jsBrowserWebpack.entry.name, jsBrowserWebpack.outputPath))
}

task run(type: JavaExec, dependsOn: [jvmJar]) {
    group = "application"
    main = "dev.mee42.MainKt"
    classpath(configurations.jvmRuntimeClasspath, jvmJar)
    args = []
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}